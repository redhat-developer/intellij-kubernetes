buildscript {
    ext.ideaVersion = ideaVersion
    /*
     * for more complex conversion ideaVersion -> sinceIdeaBuild
     * see https://github.com/rhdunn/xquery-intellij-plugin/blob/master/build.gradle#L1-L47
     */
    def since = ideaVersion =~ /IC-20(\d\d)\.([1-3])(\.\d+)?$/
    if (since.matches()) {
        ext.sinceIdeaBuild = "${since.group(1)}${since.group(2)}".toInteger()
    }

    if (ext.sinceIdeaBuild >= 223) {
        ext.kotlinVersion = kotlinVersionIdea221
        ext.kotlinStdlib = kotlinStdlibIdea221
        ext.java_version = "17"
    } else if (ext.sinceIdeaBuild >= 221) {
        ext.kotlinVersion = kotlinVersionIdea221
        ext.kotlinStdlib = kotlinStdlibIdea221
        ext.java_version = "11"
    } else {
        ext.kotlinVersion = kotlinVersion
        ext.kotlinStdlib = kotlinStdlib
        ext.java_version = "11"
    }
}

plugins {
    id "org.jetbrains.intellij" version "${intellijPluginVersion}"
    id "org.jetbrains.kotlin.jvm" version "${kotlinVersion}"
    id "idea"
}

sourceSets {
    integrationTest {
        java.srcDir file('src/it/java')
        resources.srcDir file('src/it/resources')
        compileClasspath += sourceSets.main.output + configurations.runtimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

task integrationTest(type: Test) {
    useJUnitPlatform()
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
}

tasks.withType(Test) {
    environment 'GRADLE_RELEASE_REPOSITORY','https://services.gradle.org/distributions'
}

dependencies {
    implementation(
            "org.jetbrains.kotlin:${kotlinStdlib}:${kotlinVersion}",
            "com.redhat.devtools.intellij:intellij-common:${intellijCommonVersion}",
            "io.fabric8:kubernetes-client:${kubernetesClientVersion}",
            "io.fabric8:kubernetes-model:${kubernetesClientVersion}",
            "io.fabric8:kubernetes-model-common:${kubernetesClientVersion}",
            "io.fabric8:openshift-client:${kubernetesClientVersion}",
            "org.apache.commons:commons-lang3:3.12.0"
    )
    testImplementation(
            "org.assertj:assertj-core:3.22.0",
            "org.mockito:mockito-inline:4.5.1",
            "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0",
            "org.jetbrains.kotlin:kotlin-test-junit"
    )
    integrationTestImplementation(
            "com.redhat.devtools.intellij:intellij-common:1.1.0",
            "com.redhat.devtools.intellij:intellij-common-ui-test-library:0.2.0"
    )
}

configurations {
    implementation {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    compileOptions {
        sourceCompatibility = 11
        targetCompatibility = 11
    }

    compileKotlin {
        kotlinOptions {
            if (java_version == "17") {
                jvmTarget = "17"
            } else {
                jvmTarget = "11"
            }
        }
    }

    compileTestKotlin {
        kotlinOptions {
            if (java_version == "17") {
                jvmTarget = "17"
            } else {
                jvmTarget = "11"
            }
        }
    }

    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

test {
    afterSuite { desc, result ->
        if (!desc.parent)
            println("${result.resultType} " +
                    "(${result.testCount} tests, " +
                    "${result.successfulTestCount} successes, " +
                    "${result.failedTestCount} failures, " +
                    "${result.skippedTestCount} skipped)")
    }
}

intellij {
    version = ideaVersion //for a full list of Jetbrains IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    pluginName = 'Kubernetes by Red Hat'
    plugins = ['yaml', 'com.redhat.devtools.intellij.telemetry:0.0.3.33' ]
    updateSinceUntilBuild = false
}

runIde {
    systemProperties['com.redhat.devtools.intellij.telemetry.mode'] = 'debug'
}

patchPluginXml {
    sinceBuild = "${sinceIdeaBuild}"
}

runPluginVerifier {
    ideVersions = [ideaVersion]
}

publishPlugin {
    token = jetBrainsToken
    channels = [jetBrainsChannel]
}

runIdeForUiTests {
    systemProperty "robot-server.port", System.getProperty("robot-server.port")
    systemProperties['com.redhat.devtools.intellij.telemetry.mode'] = 'debug'
}

task generateSchemas {
    final LATEST_VERSION_SCHEMA = "v1.24.2"
    final SCHEMA_BASEURL = "https://raw.githubusercontent.com/kubernetes/kubernetes"
    final SCHEMA_DIR = "src/main/resources/schemas/"
    final K8S_DIR = "k8s.io"

    doLast {
        def schemaVersion = getProjectProperty("schemaVersion", LATEST_VERSION_SCHEMA)
        def url = "${SCHEMA_BASEURL}/${schemaVersion}/api/openapi-spec/swagger.json"
        def destination = "${SCHEMA_DIR}/${K8S_DIR}"
        file("${projectDir}/${destination}").deleteDir()

        println("Generating schemas ${schemaVersion} from openapi at ${url} \ninto ${SCHEMA_DIR}")
        /*
         * openapi2jsonschema -o src/main/resources/schemas/k8s.io --kubernetes --expanded --stand-alone --strict https://raw.githubusercontent.com/kubernetes/kubernetes/v1.20.5/api/openapi-spec/swagger.json
         */
        def process = exec {
            executable 'openapi2jsonschema'
            args '-o', destination, "--kubernetes", "--expanded", "--stand-alone", "--strict", url
        }
        if (isSuccess(process.exitValue)) {
            createIndexFile("${projectDir}/${destination}")
        } else {
            println(colorize("yellow",
                    """Generating schema failed. Please make sure that you have 'openapi2jsonschema' installed.
                    Make sure you have python installed and then run 'pip install openapi2jsonschema'.
                    Further info is available at https://pypi.org/project/openapi2jsonschema/"""))
        }
    }

    ext.createIndexFile = { destination ->
        def indexFilename = "index.txt"
        def index = file("${destination}/${indexFilename}")
        index.createNewFile()
        files { file("${destination}").listFiles() }
                .filter { file ->
                    !file.name.endsWith(indexFilename) ||
                            !file.name.endsWith("all.json") ||
                            !file.name.endsWith("_definitions.json") ||
                            !file.name.contains(".v")
                }
                .each { file ->
                    index.text += "${file.name}\n"
                }
        return index
    }

    ext.isSuccess = { exitValue ->
        return exitValue == 0
    }
}

private def getProjectProperty(String key, String defaultValue) {
    def value = defaultValue
    if (project.hasProperty(key)) {
        schemaVersion = project.getProperty(key)
    }
    return value;
}

private def colorize(String color, String message) {
    final colors = [
            black: 30,
            red: 31,
            green: 32,
            yellow: 33,
            blue: 34,
            magenta: 35,
            cyan: 36,
            white: 37
    ]
    return new String((char) 27) + "[${colors[color]}m${message}" + new String((char) 27) + "[0m"
}

repositories {
    mavenCentral()
    maven {
        url 'https://repository.jboss.org'
    }
    maven {
        url 'https://packages.jetbrains.team/maven/p/ij/intellij-dependencies'
    }
}

group 'com.redhat.devtools.intellij'
version projectVersion