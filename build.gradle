buildscript {
    /**
     * Returns sinceBuild for a given IC- version.
     *
     * Supported cases:
     * (1) Year and Version -- (a) IC-2017.3, (b) 2017.2.4, (c) 2018.1
     * (2) Build ID -- (a) IU-163.7890, (b) 171.1234
     * (3) Named Snapshots -- (a) LATEST-EAP-SNAPSHOT, (b) LATEST-TRUNK-SNAPSHOT
     *
     * Taken and adapted from https://github.com/rhdunn/xquery-intellij-plugin/blob/master/build.gradle#L1-L47
     */
    ext.getSinceIdeaBuild = { version ->
        // (1) Year and Version
        def since = version =~ /([A-Z]+-)?20([0-9][0-9])\.([1-3])(\.[0-9]+)?$/
        if (since.matches()) {
            return "${since.group(2)}${since.group(3)}"
        }
        // (2) Build ID
        since = version =~ /([A-Z]+-)?([0-9][0-9][0-9])\.[0-9]+?$/
        if (since.matches()) {
            return since.group(2)
        }
        // (3) Named Snapshots
        def repositoryPath = "https://www.jetbrains.com/intellij-repository/snapshots/com/jetbrains/intellij/idea/BUILD"
        def build = new File("$projectDir/build/BUILD-${version}.txt")
        if (!build.exists()) {
            build.parentFile.mkdirs()
            println "Downloading build version file '$repositoryPath/$version/BUILD-${version}.txt' to '${build.absolutePath}'"
            new URL("$repositoryPath/$version/BUILD-${version}.txt").withInputStream { input ->
                build.withOutputStream { output -> (output << input).close() }
            }
        }
        return build.readLines()[0].split('\\.')[0]
    }

    ext.ideaVersion = ideaVersion
    ext.sinceIdeaBuild = getSinceIdeaBuild(ext.ideaVersion).toInteger()

    if (ext.sinceIdeaBuild >= 203) {
        ext.kotlinVersion = kotlinVersionIdea203
        ext.kotlinStdlib = kotlinStdlibIdea203
        ext.java_version = "11"
    } else {
        ext.kotlinVersion = kotlinVersion
        ext.kotlinStdlib = kotlinStdlib
        ext.java_version = "1.8"
    }
}

plugins {
    id "org.jetbrains.intellij" version "${intellijPluginVersion}"
    id "org.jetbrains.kotlin.jvm" version "${kotlinVersion}"
    id "idea"
}

downloadRobotServerPlugin.version = remoteRobotVersion

sourceSets {
    integrationTest {
        java.srcDir file('src/it/java')
        resources.srcDir file('src/it/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
}

tasks.withType(Test) {
    environment 'GRADLE_RELEASE_REPOSITORY','https://services.gradle.org/distributions'
}

dependencies {
    implementation(
            "org.jetbrains.kotlin:${kotlinStdlib}:${kotlinVersion}",
            "com.redhat.devtools.intellij:intellij-common:${intellijCommonVersion}",
            "io.fabric8:kubernetes-client:${kubernetesClientVersion}",
            "io.fabric8:kubernetes-model:${kubernetesClientVersion}",
            "io.fabric8:kubernetes-model-common:${kubernetesClientVersion}",
            "io.fabric8:openshift-client:${kubernetesClientVersion}",
            "org.apache.commons:commons-lang3:3.10"
    )
    testImplementation(
            "org.assertj:assertj-core:3.19.0",
            "org.mockito:mockito-inline:3.9.0",
            "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0",
            "org.jetbrains.kotlin:kotlin-test-junit"
    )
    integrationTestImplementation(
            "com.intellij.remoterobot:remote-robot:${remoteRobotVersion}",
            "com.intellij.remoterobot:remote-fixtures:${fixturesVersion}",
            "org.junit.jupiter:junit-jupiter-api:5.3.1"
    )
    integrationTestRuntimeOnly(
            "org.junit.jupiter:junit-jupiter-engine:5.3.1"
    )
}

configurations {
    compile {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

test {
    afterSuite { desc, result ->
        if (!desc.parent)
            println("${result.resultType} " +
                    "(${result.testCount} tests, " +
                    "${result.successfulTestCount} successes, " +
                    "${result.failedTestCount} failures, " +
                    "${result.skippedTestCount} skipped)")
    }
}

intellij {
    version ideaVersion //for a full list of Jetbrains IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    pluginName 'Kubernetes by Red Hat'
    updateSinceUntilBuild false
    plugins 'yaml'
}

patchPluginXml {
    sinceBuild sinceIdeaBuild
}

publishPlugin {
    token    jetBrainsToken
    channels jetBrainsChannel
}

runIdeForUiTests {
    systemProperty "robot-server.port", "8082" // default port 8080
    systemProperties['com.redhat.devtools.intellij.telemetry.mode'] = 'debug'
}

integrationTest {
    useJUnitPlatform()
}

task generateSchemas {
    final LATEST_VERSION_SCHEMA = "v1.20.5"
    final SCHEMA_BASEURL = "https://raw.githubusercontent.com/kubernetes/kubernetes"
    final SCHEMA_DIR = "src/main/resources/schemas/"
    final K8S_DIR = "k8s.io"

    doLast {
        def schemaVersion = getProjectProperty("schemaVersion", LATEST_VERSION_SCHEMA)
        def url = "${SCHEMA_BASEURL}/${schemaVersion}/api/openapi-spec/swagger.json"
        def destination = "${SCHEMA_DIR}/${K8S_DIR}"
        file("${projectDir}/${destination}").deleteDir()

        println("Generating schemas ${schemaVersion} from openapi at ${url} \ninto ${SCHEMA_DIR}")
        /*
         * openapi2jsonschema -o src/main/resources/schemas/k8s.io --kubernetes --expanded --stand-alone --strict https://raw.githubusercontent.com/kubernetes/kubernetes/v1.20.5/api/openapi-spec/swagger.json
         */
        def process = exec {
            executable 'openapi2jsonschema'
            args '-o', destination, "--kubernetes", "--expanded", "--stand-alone", "--strict", url
        }
        if (isSuccess(process.exitValue)) {
            createIndexFile("${projectDir}/${destination}")
        } else {
            println(colorize("yellow",
                    """Generating schema failed. Please make sure that you have 'openapi2jsonschema' installed.
                    Make sure you have python installed and then run 'pip install openapi2jsonschema'.
                    Further info is available at https://pypi.org/project/openapi2jsonschema/"""))
        }
    }

    ext.createIndexFile = { destination ->
        def indexFilename = "index.txt"
        def index = file("${destination}/${indexFilename}")
        index.createNewFile()
        files { file("${destination}").listFiles() }
                .filter { file ->
                    !file.name.endsWith(indexFilename) ||
                            !file.name.endsWith("all.json") ||
                            !file.name.endsWith("_definitions.json") ||
                            !file.name.contains(".v")
                }
                .each { file ->
                    index.text += "${file.name}\n"
                }
        return index
    }

    ext.isSuccess = { exitValue ->
        return exitValue == 0
    }
}

private def getProjectProperty(String key, String defaultValue) {
    def value = defaultValue
    if (project.hasProperty(key)) {
        schemaVersion = project.getProperty(key)
    }
    return value;
}

private def colorize(String color, String message) {
    final colors = [
            black: 30,
            red: 31,
            green: 32,
            yellow: 33,
            blue: 34,
            magenta: 35,
            cyan: 36,
            white: 37
    ]
    return new String((char) 27) + "[${colors[color]}m${message}" + new String((char) 27) + "[0m"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://repository.jboss.org'
    }
    maven {
        url 'https://packages.jetbrains.team/maven/p/ij/intellij-dependencies'
    }
}
    
group 'com.redhat.devtools.intellij'
version projectVersion
